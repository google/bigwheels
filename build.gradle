ext {
    assetsPath = '../../assets'
    thirdPartyAssetsPath = '../../third_party/assets'
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }

    apply plugin: "com.android.application"

    dependencies {
        implementation 'androidx.appcompat:appcompat:1.5.1'
        implementation 'com.google.android.material:material:1.7.0'
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.1.4'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
    }

    buildDir = "../../build/android/${project.name}"

    android {
        namespace 'com.google.bigwheels'
        compileSdkVersion 32

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        buildFeatures {
            prefab true
        }
        externalNativeBuild {
            cmake {
                path file('../../CMakeLists.txt')
                version '3.22.1+'
                buildStagingDirectory "../../.cxx/${project.name}"
            }
        }

        buildTypes {
            release {
                minifyEnabled false
            }
            debug {
              applicationIdSuffix '.debug'
              debuggable true
            }
        }

        defaultConfig {
            applicationId "com.google.bigwheels.project_${project.name}"
            minSdk 29
            targetSdk 32
            versionCode 1
            versionName "1.0"

            manifestPlaceholders = [
                sampleLibraryName : "vk_${project.name}",
                appLabel : "vk_${project.name}"
            ]

            ndk.abiFilters = ['arm64-v8a']

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            externalNativeBuild {
                cmake {
                    if (project.name.contains("_xr")) {
                        arguments '-DPPX_ANDROID=TRUE',
                                  '-DPPX_BUILD_TESTS=FALSE',
                                  '-DBUILD_TESTS=FALSE', // Required for OpenXR
                                  '-DPPX_BUILD_XR=TRUE',
                                  '-DBUILD_LOADER=TRUE',
                                  '-DBUILD_ALL_EXTENSIONS=TRUE'
                    }
                    else {
                        arguments '-DPPX_ANDROID=TRUE',
                                  '-DPPX_BUILD_TESTS=FALSE',
                                  '-DBUILD_TESTS=FALSE' // Required for OpenXR
                    }
                    if (project.name.contains("_xr")) {
                        cppFlags '-std=c++20',
                                 '-DXR_USE_PLATFORM_ANDROID'
                    }
                    else {
                        cppFlags '-std=c++20'
                    }
                   
                    if (project.hasProperty('DXC_PATH')) {
                      arguments "-DDXC_PATH=$DXC_PATH"
                    }
                    targets "vk_${project.name}"
                }
            }
            multiDexEnabled true
            it.buildConfigField "String", "sample_library_name", '"vk_' + project.name + '"'
            sourceSets.main {
                java.srcDirs = [ '../../src/android' ]
                assets.srcDirs = ['assets']
                if (project.name.contains("_xr")) {
                    manifest.srcFile '../../src/android/AndroidManifest.XR.xml'
                } else {
                    manifest.srcFile '../../src/android/AndroidManifest.xml'
                }
            }
        }
    }

    // Each sample project must define copyTask for copying assets
    // that are used in the project.
    preBuild.dependsOn ":" + project.name + ":copyTask"
}
