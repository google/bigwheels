plugins {
    id 'com.android.application' version '7.3.1'
    id 'com.android.library' version '7.3.1' apply false
}

android {
    namespace 'com.google.bigwheels'
    compileSdk 32

    defaultConfig {
        applicationId "com.google.bigwheels"
        minSdk 29
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags '-std=c++20'
                if (project.hasProperty('DXC_PATH')) {
                  arguments "-DDXC_PATH=$DXC_PATH"
                }
            }
        }
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    buildTypes.each {
        // The same string needs to be defined for both the manifest and
        // the MainActivity. Maybe there is a better way.
        it.buildConfigField "String", "sample_library_name", '"vk_16_gbuffer"'
        it.resValue         "string", "sample_library_name",  'vk_16_gbuffer'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        prefab true
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version '3.22.1'
        }
    }
    sourceSets {
        main {
            java.srcDirs = [ 'src/android' ]
            manifest.srcFile 'src/android/AndroidManifest.xml'
            assets {
                srcDirs 'assets'
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    implementation 'androidx.games:games-activity:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}
